#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = src.main.java.gals
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
DIGITO : [0-9]
INTEIRO: [1-9]
LETRA : [a-zA-Z]
BARRA: "/"
LINE_COMMENT: {BARRA}{BARRA}[^\n]*


#Tokens
//Ignorar caracteres de formata√ß√£o
:[\t\n\s]
//fim
//comentario
:!{LINE_COMMENT}
:!{BARRA}\* ( [^\*] | (\*)+ [^\*/] )* ( \* )+{BARRA}
//fim
//tokens 
IDENTIFICADOR: ({LETRA}) (({LETRA}|{DIGITO})| (_{LETRA}+) | (_{DIGITO}+))*_?
CONSTANTE_INTEIRA: (0 | {INTEIRO}|{INTEIRO}{DIGITO}*)
CONSTANTE_REAL: (0 | {INTEIRO}|{INTEIRO}{DIGITO}*)(,{DIGITO}+)
CONSTANTE_CARACTER: \"([^\n\\\"] | \\ (\"|\\) )* \"


//palavras reservada
BOOL = IDENTIFICADOR : "bool"
CONSTS = IDENTIFICADOR : "consts"
DEF = IDENTIFICADOR : "def"
END = IDENTIFICADOR : "end"
EXECUTE = IDENTIFICADOR : "execute"
FALSE = IDENTIFICADOR : "false"
FLOAT = IDENTIFICADOR : "float"
GET = IDENTIFICADOR : "get"
IFFALSE = IDENTIFICADOR : "ifFalse"
IFTRUE = IDENTIFICADOR : "ifTrue"
INPUT = IDENTIFICADOR : "input"
INT = IDENTIFICADOR : "int"
PRINT = IDENTIFICADOR : "print"
PRINTLN = IDENTIFICADOR : "println"
SET = IDENTIFICADOR : "set"
STR = IDENTIFICADOR : "str"
TRUE = IDENTIFICADOR : "true"
TYPES = IDENTIFICADOR : "types"
VAR = IDENTIFICADOR : "var"
WHILEFALSE = IDENTIFICADOR : "whileFalse"
WHILETRUE = IDENTIFICADOR : "whileTrue"

//simbolos especiais
"+"
"-"
"*"
"/"
":"
"("
")"
"="
":="
","
"&&"
"||"
"!"
"!="
"<"
"<="
">"
">="

#NonTerminals
<PROGRAMA> 
<EXPRESSAO>
<ELEMENTO>
<RELACIONAL>
<OPERADOR_RELACIONAL>
<ARITMETICA>
<TERMO>
<FATOR>                  
<DEFINICAO_DE_TIPOS>         
<TIPOS>                      
<DEF_TIPO>                   
<LISTA_DE_COMANDOS>          
<COMANDO>                    
<TIPO>                       
<SIMPLES>                    
<COMPOSTO>                   
<LISTA_DE_CAMPOS>            
<CAMPO>                      
<LISTA_DE_IDENTIFICADORES>   
<VALOR>                      
<DECLARACAO_DE_VARIAVEIS>    
<DEC_VAR>                    
<TIPO_VAR>                                 
<ENTRADA_DADOS>              
<SAIDA_DADOS>                
<LISTA_DE_EXPRESSOES>        
<WHILE>                      
<REP_CONSTS>                 
<DEFINICAO_DE_CONSTANTES>
<TIPOS1>
<LISTA_DE_COMANDOS1>
<EXPRESSAO1>
<ARITMETICA1>
<TERMO1>
<COMANDO1>
<ATRIBUICAO1>
<MANIPULACAO_TIPOS_COMPOSTOS1>
<SELECAO1>
<SELECAO0>
<REPETICAO1>
<COMANDO2>
<LISTA_DE_CAMPOS1>
<LISTA_DE_IDENTIFICADORES1>
<DEC_VAR1>
<LISTA_DE_EXPRESSOES1>
<SELECAO2>
<RELACIONAL1>
<FATOR1>
#Grammar
<PROGRAMA> ::= #15 DEF <DEFINICAO_DE_TIPOS> <DEFINICAO_DE_CONSTANTES> <DECLARACAO_DE_VARIAVEIS> EXECUTE <LISTA_DE_COMANDOS> #16;
<DEFINICAO_DE_TIPOS> ::= TYPES <TIPOS> | Ó;

<TIPOS> ::= <DEF_TIPO> <TIPOS1> ;
<TIPOS1> ::= <TIPOS> | Ó;

<DEF_TIPO> ::= IDENTIFICADOR ":" <TIPO>;

<LISTA_DE_COMANDOS> ::= <COMANDO> <LISTA_DE_COMANDOS1>;
<LISTA_DE_COMANDOS1> ::= <LISTA_DE_COMANDOS> | Ó;

<COMANDO> ::= IDENTIFICADOR <COMANDO1>  | <ENTRADA_DADOS> | <SAIDA_DADOS> | #27 "(" <EXPRESSAO> ")" <COMANDO2>;
<COMANDO1> ::=  <ATRIBUICAO1> | <MANIPULACAO_TIPOS_COMPOSTOS1>;
<COMANDO2> ::=  <SELECAO0> | <REPETICAO1>;
<ATRIBUICAO1> ::= #22 ":=" <EXPRESSAO> #26;
<MANIPULACAO_TIPOS_COMPOSTOS1> ::= SET "("IDENTIFICADOR"," <EXPRESSAO>")";
<TIPO> ::= <SIMPLES> | <COMPOSTO>;
<SIMPLES> ::= INT | FLOAT | STR | BOOL;
<COMPOSTO> ::= "(" <LISTA_DE_CAMPOS> ")";
<LISTA_DE_CAMPOS> ::=  <CAMPO> <LISTA_DE_CAMPOS1>;
<LISTA_DE_CAMPOS1> ::= <LISTA_DE_CAMPOS>  | Ó;
<CAMPO> ::= IDENTIFICADOR ":" <SIMPLES>;
<DEFINICAO_DE_CONSTANTES> ::= CONSTS <LISTA_DE_IDENTIFICADORES> "=" <VALOR> #32 <REP_CONSTS> | Ó;
<LISTA_DE_IDENTIFICADORES> ::= IDENTIFICADOR #22 <LISTA_DE_IDENTIFICADORES1>;
<LISTA_DE_IDENTIFICADORES1> ::= "," <LISTA_DE_IDENTIFICADORES> | Ó;
<VALOR> ::= CONSTANTE_INTEIRA | CONSTANTE_REAL | CONSTANTE_CARACTER | TRUE | FALSE;
<DECLARACAO_DE_VARIAVEIS> ::= VAR <DEC_VAR>  | Ó;
<DEC_VAR> ::= <LISTA_DE_IDENTIFICADORES> ":" <TIPO_VAR> #21 #23 <DEC_VAR1>;
<DEC_VAR1> ::= <DEC_VAR> | Ó;
<TIPO_VAR> ::= INT | FLOAT | STR | BOOL | IDENTIFICADOR;

<ENTRADA_DADOS> ::= INPUT "(" <LISTA_DE_IDENTIFICADORES> #24 ")";
<SAIDA_DADOS> ::= PRINT "(" <LISTA_DE_EXPRESSOES> ")" | PRINTLN "(" <LISTA_DE_EXPRESSOES> ")" #17;
<LISTA_DE_EXPRESSOES> ::= <EXPRESSAO> #14 <LISTA_DE_EXPRESSOES1>;
<LISTA_DE_EXPRESSOES1> ::= "," <LISTA_DE_EXPRESSOES> | Ó;

<SELECAO0> ::= <SELECAO1>;

<SELECAO1> ::= IFTRUE #28 ":" <LISTA_DE_COMANDOS> <SELECAO2> END #29 | IFFALSE #28 ":" <LISTA_DE_COMANDOS> END #29;
<SELECAO2> ::= #30 IFFALSE ":" <LISTA_DE_COMANDOS> | Ó;
<REPETICAO1> ::= <WHILE>":" <LISTA_DE_COMANDOS> END #31;
<WHILE> ::= WHILETRUE #28 | WHILEFALSE #28;

<EXPRESSAO> ::= <ELEMENTO> <EXPRESSAO1>;
<EXPRESSAO1> ::= "&&" <ELEMENTO> #18 <EXPRESSAO1> | "||" <ELEMENTO> #19 <EXPRESSAO1> | Ó;

<ELEMENTO> ::= <RELACIONAL> | TRUE #11 | FALSE #12 |  "!"<ELEMENTO> #13;
<RELACIONAL> ::= <ARITMETICA><RELACIONAL1>;
<RELACIONAL1> ::= <OPERADOR_RELACIONAL> #9 <ARITMETICA> #10 | Ó;
<OPERADOR_RELACIONAL> ::=  "=" |  "!=" |  "<" | "<=" | ">" | ">=" ;

<ARITMETICA> ::= <TERMO> <ARITMETICA1>;
<ARITMETICA1> ::= "+" <TERMO> #1 <ARITMETICA1> | "-" <TERMO> #2 <ARITMETICA1> | Ó;

<TERMO> ::= <FATOR> <TERMO1>;
<TERMO1> ::= "*" <FATOR> #3 <TERMO1> | "/" <FATOR> #4 <TERMO1> | Ó;


<FATOR> ::= IDENTIFICADOR #25 <FATOR1> | CONSTANTE_INTEIRA #5 | CONSTANTE_REAL #6 | CONSTANTE_CARACTER #20 |  "(" <EXPRESSAO> ")" | "+" <FATOR> #7 |  "-" <FATOR> #8;
<FATOR1> ::= GET "(" IDENTIFICADOR ")" | Ó;
<REP_CONSTS> ::= <LISTA_DE_IDENTIFICADORES> "=" <VALOR> <REP_CONSTS> | Ó;
