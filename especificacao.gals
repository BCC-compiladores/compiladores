#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = src.main.java.gals
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
DIGITO : [0-9]
INTEIRO: [1-9]
LETRA : [a-zA-Z]
BARRA: "/"
LINE_COMMENT: {BARRA}{BARRA}[^\n]*


#Tokens
//Ignorar caracteres de formatação
:[\t\n\s]
//fim
//comentario
:!{LINE_COMMENT}
:!{BARRA}\* ( [^\*] | (\*)+ [^\*/] )* ( \* )+{BARRA}
//fim
//tokens 
IDENTIFICADOR: ({LETRA}) (({LETRA}|{DIGITO})| (_{LETRA}+) | (_{DIGITO}+))*_?
CONSTANTE_INTEIRA: (0 | {INTEIRO}|{INTEIRO}{DIGITO}*)
CONSTANTE_REAL: (0 | {INTEIRO}|{INTEIRO}{DIGITO}*)(,{DIGITO}+)
CONSTANTE_CARACTER: \"([^\n\\\"] | \\ (\"|\\) )* \"


//palavras reservada
BOOL = IDENTIFICADOR : "bool"
CONSTS = IDENTIFICADOR : "consts"
DEF = IDENTIFICADOR : "def"
END = IDENTIFICADOR : "end"
EXECUTE = IDENTIFICADOR : "execute"
FALSE = IDENTIFICADOR : "false"
FLOAT = IDENTIFICADOR : "float"
GET = IDENTIFICADOR : "get"
IFFALSE = IDENTIFICADOR : "ifFalse"
IFTRUE = IDENTIFICADOR : "ifTrue"
INPUT = IDENTIFICADOR : "input"
INT = IDENTIFICADOR : "int"
PRINT = IDENTIFICADOR : "print"
PRINTLN = IDENTIFICADOR : "println"
SET = IDENTIFICADOR : "set"
STR = IDENTIFICADOR : "str"
TRUE = IDENTIFICADOR : "true"
TYPES = IDENTIFICADOR : "types"
VAR = IDENTIFICADOR : "var"
WHILEFALSE = IDENTIFICADOR : "whileFalse"
WHILETRUE = IDENTIFICADOR : "whileTrue"

//simbolos especiais
"+"
"-"
"*"
"/"
":"
"("
")"
"="
":="
","
"&&"
"||"
"!"
"!="
"<"
"<="
">"
">="

#NonTerminals
<PROGRAMA> 
<EXPRESSAO>
<ELEMENTO>
<RELACIONAL>
<OPERADOR_RELACIONAL>
<ARITMETICA>
<TERMO>
<FATOR>                  
<DEFINICAO_DE_TIPOS>         
<TIPOS>                      
<DEF_TIPO>                   
<LISTA_DE_COMANDOS>          
<COMANDO>                    
<TIPO>                       
<SIMPLES>                    
<COMPOSTO>                   
<LISTA_DE_CAMPOS>            
<CAMPO>                      
<CONSTANTE>                  
<LISTA_DE_IDENTIFICADORES>   
<VALOR>                      
<DECLARACAO_DE_VARIAVEIS>    
<DEC_VAR>                    
<TIPO_VAR>                   
<ATRIBUICAO>                 
<MANIPULACAO_TIPOS_COMPOSTOS>
<ENTRADA_DADOS>              
<SAIDA_DADOS>                
<LISTA_DE_EXPRESSOES>        
<SELECAO>                    
<IF>                         
<REPETICAO>                  
<WHILE>                      
<REP_CONSTS>                 
<DEFINICAO_DE_CONSTANTES>
<TIPOS1>
<LISTA_DE_COMANDOS1>
<EXPRESSAO1>
<ARITMETICA1>
<TERMO1>
<COMANDO1>
<ATRIBUICAO1>
<MANIPULACAO_TIPOS_COMPOSTOS1>
<SELECAO1>
<SELECAO0>
<REPETICAO1>
<COMANDO2>
<LISTA_DE_CAMPOS1>
<LISTA_DE_IDENTIFICADORES1>
<DEC_VAR1>
<LISTA_DE_EXPRESSOES1>
#Grammar
<PROGRAMA> ::= DEF <DEFINICAO_DE_TIPOS> <DEFINICAO_DE_CONSTANTES> <DECLARACAO_DE_VARIAVEIS> EXECUTE <LISTA_DE_COMANDOS>;
<DEFINICAO_DE_TIPOS> ::= TYPES <TIPOS> | î;

<TIPOS> ::= <DEF_TIPO> <TIPOS1> ;
<TIPOS1> ::= <TIPOS> | î;

<DEF_TIPO> ::= IDENTIFICADOR ":" <TIPO>;

<LISTA_DE_COMANDOS> ::= <COMANDO> <LISTA_DE_COMANDOS1>;
<LISTA_DE_COMANDOS1> ::= <LISTA_DE_COMANDOS> | î;

<COMANDO> ::= IDENTIFICADOR <COMANDO1>  | <ENTRADA_DADOS> | <SAIDA_DADOS> | "(" <EXPRESSAO> ")" <COMANDO2>;
<COMANDO1> ::=  <ATRIBUICAO1> | <MANIPULACAO_TIPOS_COMPOSTOS1>;
<COMANDO2> ::=  <SELECAO0> | <REPETICAO1>;
<ATRIBUICAO1> ::= ":=" <EXPRESSAO>;
<MANIPULACAO_TIPOS_COMPOSTOS1> ::= SET "("IDENTIFICADOR"," <EXPRESSAO>")";
<TIPO> ::= <SIMPLES> | <COMPOSTO>;
<SIMPLES> ::= INT | FLOAT | STR | BOOL;
<COMPOSTO> ::= "(" <LISTA_DE_CAMPOS> ")";
<LISTA_DE_CAMPOS> ::=  <CAMPO> <LISTA_DE_CAMPOS1>;
<LISTA_DE_CAMPOS1> ::= <LISTA_DE_CAMPOS>  | î;
<CAMPO> ::= IDENTIFICADOR ":" <SIMPLES>;
<DEFINICAO_DE_CONSTANTES> ::= CONSTS <LISTA_DE_IDENTIFICADORES> "=" <VALOR> <REP_CONSTS> | î;
<LISTA_DE_IDENTIFICADORES> ::= IDENTIFICADOR <LISTA_DE_IDENTIFICADORES1>;
<LISTA_DE_IDENTIFICADORES1> ::= "," <LISTA_DE_IDENTIFICADORES> | î;
<VALOR> ::= CONSTANTE_INTEIRA | CONSTANTE_REAL | CONSTANTE_CARACTER | TRUE | FALSE;
<DECLARACAO_DE_VARIAVEIS> ::= VAR <DEC_VAR>  | î;
<DEC_VAR> ::= <LISTA_DE_IDENTIFICADORES> ":" <TIPO_VAR> <DEC_VAR1>;
<DEC_VAR1> ::= <DEC_VAR> | î;
<TIPO_VAR> ::= INT | FLOAT | STR | BOOL | IDENTIFICADOR;

<ATRIBUICAO> ::= IDENTIFICADOR ":=" <EXPRESSAO>;

<MANIPULACAO_TIPOS_COMPOSTOS> ::= IDENTIFICADOR SET "("IDENTIFICADOR"," <EXPRESSAO>")";

<ENTRADA_DADOS> ::= INPUT "(" <LISTA_DE_IDENTIFICADORES> ")";
<SAIDA_DADOS> ::= PRINT "(" <LISTA_DE_EXPRESSOES> ")" | PRINTLN "(" <LISTA_DE_EXPRESSOES> ")";
<LISTA_DE_EXPRESSOES> ::= <EXPRESSAO> <LISTA_DE_EXPRESSOES1>;
<LISTA_DE_EXPRESSOES1> ::= "," <LISTA_DE_EXPRESSOES> | î;

<SELECAO0> ::= <SELECAO1>;
<SELECAO> ::= "(" <EXPRESSAO> ")"  <SELECAO1>;
<SELECAO1> ::= <IF> ":" <LISTA_DE_COMANDOS> END | IFTRUE ":" <LISTA_DE_COMANDOS> IFFALSE ":" <LISTA_DE_COMANDOS> END;
<IF> ::= IFTRUE | IFFALSE;
<REPETICAO> ::= "(" <EXPRESSAO> ")" <WHILE>":" <LISTA_DE_COMANDOS> END;
<REPETICAO1> ::= <WHILE>":" <LISTA_DE_COMANDOS> END;
<WHILE> ::= WHILETRUE | WHILEFALSE;

<EXPRESSAO> ::= <ELEMENTO> <EXPRESSAO1>;
<EXPRESSAO1> ::= "&&" <ELEMENTO> <EXPRESSAO1> | "||" <ELEMENTO> <EXPRESSAO1> | î;

<ELEMENTO> ::= <RELACIONAL> | TRUE | FALSE |  "!"<ELEMENTO>;
<RELACIONAL> ::= <ARITMETICA> <OPERADOR_RELACIONAL> <ARITMETICA> | <ARITMETICA>;
<OPERADOR_RELACIONAL> ::=  "=" |  "!=" |  "<" | "<=" | ">" | ">=" ;

<ARITMETICA> ::= <TERMO> <ARITMETICA1>;
<ARITMETICA1> ::= "+" <TERMO> <ARITMETICA1> | "-" <TERMO> <ARITMETICA1> | î;

<TERMO> ::= <FATOR> <TERMO1>;
<TERMO1> ::= "*" <FATOR> <TERMO1> | "/" <FATOR> <TERMO1> | î;


<FATOR> ::= IDENTIFICADOR |  IDENTIFICADOR GET "(" IDENTIFICADOR ")" | CONSTANTE_INTEIRA | CONSTANTE_REAL | CONSTANTE_CARACTER |  "(" <EXPRESSAO> ")" | "+" <FATOR> |  "-" <FATOR>;
<REP_CONSTS> ::= <LISTA_DE_IDENTIFICADORES> "=" <VALOR> <REP_CONSTS> | î;
